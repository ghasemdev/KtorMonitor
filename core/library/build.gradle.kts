import org.jetbrains.kotlin.gradle.ExperimentalWasmDsl
import com.android.build.gradle.internal.cxx.configure.gradleLocalProperties
import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
    alias(libs.plugins.kotlinMultiplatform)
    alias(libs.plugins.androidLibrary)
    alias(libs.plugins.composeMultiplatform)
    alias(libs.plugins.composeCompiler)
    alias(libs.plugins.kotlinxSerialization)
    alias(libs.plugins.sqldelight)
    alias(libs.plugins.kotlinx.atomicfu)
    alias(libs.plugins.koin.compiler)
    alias(libs.plugins.maven.publish)
    alias(libs.plugins.binary.compatibility.validator)
    alias(libs.plugins.dokka)
    id("maven-publish")
}

sqldelight {
    databases {
        create("LibraryDatabase") {
            packageName.set("ro.cosminmihu.ktor.monitor.db.sqldelight")
            generateAsync = true
        }
    }
    linkSqlite = true
}

compose.resources {
    publicResClass = true
    generateResClass = always
    packageOfResClass = "ro.cosminmihu.ktor.monitor.ui.resources"
}

dokka {
    moduleName = "Ktor Monitor"
    moduleVersion = project.version.toString()

    dokkaSourceSets.configureEach {
        suppressedFiles.from(
            file("src/commonMain/kotlin/ro/cosminmihu/ktor/monitor/InternalLibraryBridge.kt")
        )

        suppressedFiles.from(
            file("src/commonMain/kotlin/ro/cosminmihu/ktor/monitor/domain/model/ClientSource.kt")
        )

        perPackageOption {
            matchingRegex.set("ro.cosminmihu.ktor.monitor.db.sqldelight")
            suppress.set(true)
        }

        perPackageOption {
            matchingRegex.set("ro.cosminmihu.ktor.monitor.ui.resources")
            suppress.set(true)
        }
    }
}

apiValidation {
    @OptIn(kotlinx.validation.ExperimentalBCVApi::class)
    klib {
        enabled = true
        strictValidation = true
    }

    publicPackages.add("ro.cosminmihu.ktor.monitor")
    ignoredClasses.add("ro.cosminmihu.ktor.monitor.InternalKtorMonitorApi")
    ignoredClasses.add("ro.cosminmihu.ktor.monitor.InternalLibraryBridge")
    ignoredClasses.add("ro.cosminmihu.ktor.monitor.domain.model.ClientSource")
    ignoredPackages.add("ro.cosminmihu.ktor.monitor.db")
    ignoredPackages.add("ro.cosminmihu.ktor.monitor.ui")
}

mavenPublishing {
    publishToMavenCentral()

    signAllPublications()

    val artifact = "ktor-monitor-core"
    coordinates(group.toString(), artifact, version.toString())

    pom {
        name.set("Ktor Monitor - Core")
        description.set("""Ktor Client plugin that provides the capability to log HTTP calls for Ktor Monitor.""".trimMargin())
        inceptionYear.set("2025")
        url.set("https://github.com/CosminMihuMDC/KtorMonitor")

        licenses {
            license {
                name = "The Apache Software License, Version 2.0"
                url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                distribution = "https://www.apache.org/licenses/LICENSE-2.0.txt"
            }
        }

        developers {
            developer {
                id = "Cosmin Mihu"
                name = "Cosmin Mihu"
                url = "https://www.cosminmihu.ro/"
            }
        }

        scm {
            url = "https://github.com/CosminMihuMDC/KtorMonitor.git"
            connection = "scm:git:git://github.com/CosminMihuMDC/KtorMonitor.git"
            developerConnection = "scm:git:git://github.com/CosminMihuMDC/KtorMonitor.git"
        }

        issueManagement {
            system = "GitHub Issues"
            url = "https://github.com/CosminMihuMDC/KtorMonitor/issues"
        }

        ciManagement {
            system = "GitHub Actions"
            url = "https://github.com/CosminMihuMDC/KtorMonitor/actions"
        }

        distributionManagement {
            downloadUrl = "https://github.com/CosminMihuMDC/KtorMonitor/releases"
        }
    }
}

val module = "ktor-monitor"
val artifactPrefix = "ktor-monitor-logging"
group = "ir.parsuomash.ktor"
version = "1.13.0"

kotlin {
    explicitApi()

    compilerOptions {
        freeCompilerArgs.add("-Xexpect-actual-classes") // TODO remove after jetbrains fix
    }

    js {
        browser()
    }

    @OptIn(ExperimentalWasmDsl::class)
    wasmJs {
        browser()
    }

    androidTarget {
        compilerOptions {
            jvmTarget.set(JvmTarget.JVM_11)
        }
    }

    listOf(
        iosArm64(),
        iosSimulatorArm64(),
    ).forEach { iosTarget ->
        iosTarget.binaries.framework {
            isStatic = true
            linkerOpts("-lsqlite3")
        }
    }

    jvm()

    sourceSets {
        androidMain.dependencies {
            implementation(libs.androidx.activity.compose)
            implementation(libs.android.permisssions)
            implementation(libs.sqldelight.android)
            implementation(libs.koin.android)
            implementation(libs.coil.gif)
            implementation(libs.core.splashscreen)
            implementation(libs.material)
        }
        iosMain.dependencies {
            implementation(libs.sqldelight.native)
        }
        commonMain.dependencies {
            implementation(libs.compose.runtime)
            implementation(libs.compose.foundation)
            implementation(libs.compose.material3)
            implementation(libs.compose.material.icons.extended)
            implementation(libs.compose.material3.adaptive.navigation.suite)
            implementation(libs.compose.ui)
            implementation(libs.compose.components.resources)
            implementation(libs.compose.ui.tooling.preview)
            implementation(libs.compose.adaptive)
            implementation(libs.compose.adaptive.layout)
            implementation(libs.compose.adaptive.navigation)
            implementation(libs.navigation.event)
            implementation(libs.androidx.lifecycle.viewmodel)
            implementation(libs.androidx.lifecycle.runtime.compose)
            implementation(libs.sqldelight.runtime)
            implementation(libs.sqldelight.coroutines)
            implementation(libs.sqldelight.primitive.adapters)
            implementation(libs.koin.core)
            implementation(libs.koin.compose)
            implementation(libs.koin.compose.viewmodel)
            implementation(libs.kotlinx.datetime)
            implementation(libs.kotlinx.serialization.json)
            implementation(libs.coil.compose)
            implementation(libs.coil.network.ktor)
            implementation(libs.coil.svg)
            implementation(libs.kotlinx.atomicfu)
            implementation(libs.ksoup)
            implementation(libs.ktor.utils)
        }
        jvmMain.dependencies {
            implementation(compose.desktop.currentOs)
            implementation(libs.kotlinx.coroutines.swing)
            implementation(libs.sqldelight.jvm)
            implementation(libs.slf4j.simple)
        }

        webMain.dependencies {
            implementation(libs.sqldelight.web)
            implementation(npm("sql.js", libs.versions.sqljs.get()))
            implementation(npm("@cashapp/sqldelight-sqljs-worker", libs.versions.sqldelight.get()))
            implementation(devNpm("copy-webpack-plugin", libs.versions.webpack.get()))
        }
    }

    jvmToolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

android {
    namespace = "ro.cosminmihu.ktor.monitor"
    compileSdk = libs.versions.android.compileSdk.get().toInt()

    defaultConfig {
        minSdk = libs.versions.android.minSdk.get().toInt()
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }
    buildTypes {
        release {
            isMinifyEnabled = false
        }
    }
}

afterEvaluate {
    publishing {
//        publications.withType<MavenPublication>().configureEach {
//            artifactId = when (name) {
//                "kotlinMultiplatform" -> artifactPrefix
//                else -> {
//                    val suffix = name
//                        .replace("Release", "")
//                        .replace("Debug", "-debug")
//                        .replaceFirstChar { it.lowercaseChar() }
//
//                    "$artifactPrefix-${suffix}"
//                }
//            }
//        }
        repositories {
            maven {
                url = uri(getProperty("maven.url"))
                credentials {
                    username = getProperty("maven.username")
                    password = getProperty("maven.password")
                }
            }
        }
    }
}

fun getProperty(key: String) = gradleLocalProperties(rootDir, providers).getProperty(key).orEmpty()
